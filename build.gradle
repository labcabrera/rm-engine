buildscript {
	ext {
		springBootVersion = "1.5.2.RELEASE"
		springCloudZuulVersion = "1.2.5.RELEASE"
		springCloudConfigVersion = "1.3.0.RELEASE"
		lombokVersion = "1.16.14"
		springfoxSwaggerVersion = "2.6.1"
		bootstrapVersion = "3.3.7"
  		angularjsVersion = "1.6.4"
	}
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

subprojects {
    apply plugin: "java"
	apply plugin: "org.springframework.boot"
	apply plugin: "eclipse"

    version = version
    group = "org.lab.rm.engine"

    compileJava {
			sourceCompatibility = 1.8
			targetCompatibility = 1.8
			options.encoding = "UTF-8"
	}
	repositories {
		mavenLocal()
		mavenCentral()
	}
    dependencies {
    	compile "org.projectlombok:lombok:${lombokVersion}"
    	testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project(":rm-engine-model") {
	dependencies {
		compile("org.springframework.boot:spring-boot-starter-data-mongodb")
		compile("io.springfox:springfox-swagger2:${springfoxSwaggerVersion}")
	}
	bootRepackage {
		enabled = false
	}
}

project(":rm-engine-core") {
	dependencies {
		compile(project(":rm-engine-model"))
	}
	bootRepackage {
		enabled = false
	}
}

project(":rm-engine-cloud-config") {
	dependencies {
		compile "org.springframework.cloud:spring-cloud-config-server:${springCloudConfigVersion}"
		compile "org.springframework.boot:spring-boot-starter-web"
	}
}

configure(subprojects.findAll {it.name.startsWith("rm-engine-ms")}) {
	dependencies {
		compile project(":rm-engine-core")
		compile "org.springframework.boot:spring-boot-starter"
		compile "org.springframework.boot:spring-boot-starter-web"
		compile "org.springframework.boot:spring-boot-starter-actuator"
		compile "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
		compile "org.springframework.cloud:spring-cloud-config-client:${springCloudConfigVersion}"
	}
}

project(":rm-engine-web") {
	dependencies {
		compile "org.springframework.boot:spring-boot-starter"
		compile "org.springframework.boot:spring-boot-starter-web"
		compile "org.springframework.boot:spring-boot-starter-thymeleaf"
		compile "org.springframework.boot:spring-boot-starter-actuator"
		compile "org.springframework.cloud:spring-cloud-starter-zuul:${springCloudZuulVersion}"
		compile "org.springframework.cloud:spring-cloud-config-client:${springCloudConfigVersion}"
		compile "org.webjars:bootstrap:${bootstrapVersion}"
		compile "org.webjars:angularjs:${angularjsVersion}"
		
		compile "org.webjars:jquery:3.2.0"
		compile "org.webjars:angular-ui-select:0.19.6"
	}
}
